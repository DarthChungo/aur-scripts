_pkgname=oggify
pkgname=${_pkgname}-stlee42-git
pkgver=0.1.0.r52.0af9905
pkgrel=1
pkgdesc="Download spotify media (Premium account required)"
arch=('i686' 'x86_64')
url="https://github.com/stlee42/oggify.git"
license=('MIT')
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
makedepends=(git rust)
optdepends=('vorbis-tools: for included tag_ogg script')
source=(${_pkgname}::git+https://github.com/stlee42/${_pkgname}.git)
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  echo "$(awk '/^version/ {print substr($3,2,length($3)-3)}' Cargo.toml).r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"
  cargo build --release
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -Dm755 target/release/oggify "$pkgdir/usr/bin/oggify"
  install -Dm755 tag_ogg "$pkgdir/usr/share/oggify/tag_ogg"
}

